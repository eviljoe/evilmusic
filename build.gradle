/////////////
// Plugins //
/////////////

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'eclipse'
apply plugin: 'spring-boot'
apply plugin: 'lesscss'

//////////////
// Settings //
//////////////

sourceCompatibility = '1.7'
version = '0.1.a'
applicationDefaultJvmArgs = ["-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=8000"]

/* Necessary to set the embedded Tomcat version. */
configurations.all {
  resolutionStrategy.eachDependency { DependencyResolveDetails details ->
    if (details.requested.group == 'org.apache.tomcat.embed') {
        details.useVersion '8.0.15'
    }
  }
}

/////////////////
// Source Sets //
/////////////////

sourceSets {
    intTest {
        java.srcDir file('src/inttest/java')
        resources.srcDir file('src/inttest/resources')
    }
}

/////////////////////////////////
// Repositories & Dependencies //
/////////////////////////////////

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile 'org.springframework:spring-context:4.1.1.RELEASE'
    compile 'org.springframework:spring-webmvc:4.1.1.RELEASE'
    compile 'org.springframework:spring-web:4.1.1.RELEASE'
    compile 'org.springframework.boot:spring-boot-starter-web:1.1.8.RELEASE'
    compile 'org.springframework.boot:spring-boot-starter-data-jpa:1.1.8.RELEASE'
    compile 'org.springframework.boot:spring-boot-autoconfigure:1.1.8.RELEASE'
    compile 'com.sun.jersey:jersey-server:1.18'
    compile 'com.sun.jersey:jersey-core:1.18'
    compile 'com.sun.jersey:jersey-servlet:1.18'
    compile 'org.apache.derby:derby:10.11.1.1'
    compile 'org.hibernate:hibernate-entitymanager:4.3.6.Final'
    compile 'commons-cli:commons-cli:1.2'

    testCompile 'junit:junit:4.11'
    testCompile 'org.easymock:easymock:3.2'
    testCompile 'com.jayway.restassured:rest-assured:2.4.0'

    intTestCompile sourceSets.main.output
    intTestCompile configurations.testCompile
    intTestCompile sourceSets.test.output
    intTestRuntime configurations.testRuntime
}

//////////////////////
// Plugin Functions //
//////////////////////

/* Necessary for the spring-boot plugin. */
buildscript {
    repositories {
        maven { url "http://repo.spring.io/libs-release" }
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        // Necessary For Spring Boot
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.1.8.RELEASE'

        // Necessary For LESS
        classpath 'de.obqo.gradle:gradle-lesscss-plugin:1.0-1.3.3'
    }
}

///////////
// Tasks //
///////////

/* Integration Test Task */
task intTest(type: Test) {
    testClassesDir = sourceSets.intTest.output.classesDir
    classpath = sourceSets.intTest.runtimeClasspath
}

/* LESS Task */
lesscss {
    source = fileTree('src/main/webapp/assets/less') {
        include '*.less'
    }
    dest = 'src/main/webapp/assets/css'
    compress = false
}

processResources {
    // 'lesscss' will be invoked every time 'processResources' is invoked
    dependsOn 'lesscss'
}

war {
    // Ensures LESS files do not make it into the WAR
    exclude 'assets/less'
}